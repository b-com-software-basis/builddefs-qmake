# Author(s) : Loic Touraine, Stephane Leduc

# Check input parameters existence
isEmpty(QMAKE_VERA) {
    unix {
        QMAKE_VERA = $$system(which vera++)
    }

    win32 {
    }

    isEmpty(QMAKE_VERA) {
        message("vera not found")
    }
}

isEmpty(QMAKE_VERA_FLAGS):!!isEmpty(QMAKE_VERA) {
    QMAKE_VERA_FLAGS = -w 3
}

!isEmpty(QMAKE_VERA) {
    SONAR_ANALYZERS += "vera"

    remaken_vera_source.CONFIG += no_link combine
    remaken_vera_source.dependency_type = TYPE_C
    remaken_vera_source.variables = QMAKE_VERA_FLAGS
    remaken_vera_source.commands = $$QMAKE_VERA $(QMAKE_COMP_QMAKE_VERA_FLAGS) -c ${QMAKE_FILE_OUT} ${QMAKE_FILE_IN}
    remaken_vera_source.output = $${_PRO_FILE_PWD_}/$${QMAKE_CODE_SCANNER_REPORT_FOLDER}/vera_report.xml
    remaken_vera_source.input = SOURCES
    remaken_vera_source.input += HEADERS
    remaken_vera_source.name = VERA
    remaken_vera_source.variable_out = BCOM_VERA_REPORT
    silent:remaken_vera_source.commands = @echo Calling vera++ $${BCOM_ALLSOURCES} && $$remaken_vera_source.commands
    QMAKE_EXTRA_COMPILERS += remaken_vera_source
}

