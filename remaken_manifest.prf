# Author(s) : Loic Touraine, Stephane Leduc

include(remaken_installpath_define.pri)

REMAKEN_TARGET_INFO=$${TARGET}-$${VERSION}_$${REMAKEN_INFO_SUFFIX}
PLATFORMFLAG="platform=$${REMAKEN_TARGET_PLATFORM}"

write_file($${OUT_PWD}/$${REMAKEN_TARGET_INFO}, PLATFORMFLAG)

contains(CONFIG,c++11) {
    CPPSTDFLAG="cppstd=11"
}
contains(CONFIG,c++14) {
    CPPSTDFLAG="cppstd=14"
}
contains(CONFIG,c++1z)|contains(CONFIG,c++17) {
    CPPSTDFLAG="cppstd=17"
}
write_file($${OUT_PWD}/$${REMAKEN_TARGET_INFO}, CPPSTDFLAG, append)

win32 {
    usestaticwinrt {
        WINRTFLAG="runtime=staticCRT"
    } else {
        WINRTFLAG="runtime=dynamicCRT"
    }
    write_file($${OUT_PWD}/$${REMAKEN_TARGET_INFO}, WINRTFLAG, append)
}


QMAKE_DISTCLEAN += $${OUT_PWD}/$${REMAKEN_TARGET_INFO}

install_remakeninfo.path = $${PROJECTDEPLOYDIR}
install_remakeninfo.files = $$files($${OUT_PWD}/$${REMAKEN_TARGET_INFO})
INSTALLS += install_remakeninfo
